# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Copyright 2016 and onwards, Google, Inc.

# Verbalizer for electronic items such as URLs and email addresses.
# These will only contain ascii characters and digits...so the rules
# are pretty much like in English.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'verbatim.grm' as verbatim;

remove_quote = "\"" : "";

# Dot, slash and colon will be transliterated in urls and emails where it is
# translated in verbatim.
dot = "." : "ඩොට්";
slash = "/" : "ස්ලෑෂ්";
colon = ":" : "කෝලන්";

electronic_verbatim =
    (("@" | "_" | "-" ) @ verbatim.MAPPINGS) | dot | slash | colon
;

# i.e. Read the abc123 part of abc123@gmail.com.
read_verbatim = universal_rules.ins_space
                ((universal_rules.kNotQuote - ".")+ @ verbatim.MAPPINGS)
                universal_rules.ins_space;

# Read extensions like html, htm, ftp.
lseq_verbatim = universal_rules.URL_LSEQS @ read_verbatim+;
number_verbatim = universal_rules.kDigit+ @ read_verbatim+;

www_dot =
  "www." : "ඩබ්ලිව් ඩබ්ලිව් ඩබ්ලිව්" universal_rules.ins_space dot universal_rules.ins_space;
# Popular sites which doesn't read letter by letter.
popular_sites =
  StringFile['urls.tsv'];

read_electronic = (
                    (
                      (
                        (www_dot<-1>)?
                        universal_rules.ins_space
                        (
                          (popular_sites <-1>) |
                          (lseq_verbatim) |
                          (number_verbatim<1>) |
                          (electronic_verbatim) |
                          (verbatim.MAPPINGS <2>)
                        )
                        universal_rules.ins_space
                      )+
                    )<1>
                  )*;

# special case to read Twitter hashtags more naturally
read_hashtag = ("#" : " හෑෂ් ")
               (
                 universal_rules.ins_space
                 (universal_rules.kAlpha+ @ universal_rules.lower_case)
                 ("_" : " ")?
                 universal_rules.ins_space
               )+;

# These domains will be read letter by letter.
lk = "lk" : "l_letter-en k_letter-en";
edu = "edu" : "e_letter-en d_letter-en u_letter-en";
ac = "ac" : "a_letter-en c_letter-en";
gov = "gov" : "g_letter-en o_letter-en v_letter-en";
io = "io" : "i_letter-en o_letter-en";
sch = "sch" : "s_letter-en c_letter-en h_letter-en";

url_suffix = (".com" : dot universal_rules.ins_space "com")
  | (".edu" : dot universal_rules.ins_space edu)
  | (".lk" : dot universal_rules.ins_space lk)
  | (".org" : dot universal_rules.ins_space "org")
  | (".net" : dot universal_rules.ins_space "net")
  | (".io" : dot universal_rules.ins_space io)
  | (".ac.lk" : dot universal_rules.ins_space ac universal_rules.ins_space dot universal_rules.ins_space lk)
  | (".sch.lk" : dot universal_rules.ins_space sch universal_rules.ins_space dot universal_rules.ins_space lk)
  | (".gov.lk" : dot universal_rules.ins_space gov universal_rules.ins_space dot universal_rules.ins_space lk)
;

read_domain = read_electronic
              ((url_suffix <-1>) |
               ((("." universal_rules.kAlpha{2}) @ read_verbatim+)<-1>)?);

# Reading of IPv6/MAC addresses; basically just skip the separating characters.
read_address = ((universal_rules.kHex @ read_verbatim) | ((":" | "-") : " sil "))+;

deal_with_dots = CDRewrite[dot, "", "", universal_rules.sigma_star];

protocol = (universal_rules.electronic_protocol
            remove_quote
            read_verbatim*
            remove_quote) @ deal_with_dots;
username = (universal_rules.electronic_username
            remove_quote
            (read_electronic | read_hashtag)
            remove_quote) @ deal_with_dots;
password = universal_rules.electronic_password
           remove_quote
           read_verbatim*
           remove_quote;
domain = (universal_rules.electronic_domain
          remove_quote
          (read_domain | (read_address<-1>))
          remove_quote) @ deal_with_dots;
# Read the port digit-by-digit
port = universal_rules.electronic_port (universal_rules.kDigit+ @ read_verbatim+);
path = universal_rules.electronic_path
       remove_quote
       read_electronic
       remove_quote;
query_string = universal_rules.electronic_query_string
               remove_quote
               read_electronic
               remove_quote;
fragment_id = universal_rules.electronic_fragment_id
              remove_quote
              read_electronic
              remove_quote;

electronic = Optimize[
  (protocol ("" : (universal_rules.ins_space
                   colon
                   universal_rules.ins_space
                   slash
                   universal_rules.ins_space
                   slash
                   universal_rules.ins_space)))?
  (username (("" : " මුරපදය ") password ("" : " ඇට් "))?)?
  (domain)?
  (("" : " පෝර්ට් ") port)?
  (path)?
  (("" : " ප්‍රශ්නාර්ථය ") query_string)?
  (("" : " හෑෂ් ") fragment_id)?
];

export ELECTRONIC_MARKUP = Optimize[
  (universal_rules.electronic
   universal_rules.opening_brace
   electronic
   universal_rules.closing_brace)
  @ universal_rules.CLEAN_SPACES
];
