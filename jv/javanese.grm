# jv/javanese.grm
# -*- Thrax -*-
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Copyright 2016 Google Inc. All Rights Reserved.
# Author: mjansche@google.com (Martin Jansche)
#
# Pronunciation rules for modern Javanese, written in Latin script.
#
# Sources:
#   https://jv.wikipedia.org/wiki/Éjaan_Basa_Jawa
#   https://ja.wikipedia.org/wiki/ジャワ語

g = SymbolTable['jv/grapheme.syms'];
p = SymbolTable['jv/phoneme.syms'];

export UTF8_TO_GRAPHEME = Optimize[
  ("'" : "'".g)
| ("-" : "-".g)
| ("a" : "a".g)
| ("b" : "b".g)
| ("c" : "c".g)
| ("d" : "d".g)
| ("e" : "e".g)
| ("f" : "f".g)
| ("g" : "g".g)
| ("h" : "h".g)
| ("i" : "i".g)
| ("j" : "j".g)
| ("k" : "k".g)
| ("l" : "l".g)
| ("m" : "m".g)
| ("n" : "n".g)
| ("o" : "o".g)
| ("p" : "p".g)
| ("q" : "q".g)
| ("r" : "r".g)
| ("s" : "s".g)
| ("t" : "t".g)
| ("u" : "u".g)
| ("v" : "v".g)
| ("w" : "w".g)
| ("x" : "x".g)
| ("y" : "y".g)
| ("z" : "z".g)
| ("â" : "â".g)
| ("å" : "å".g)
| ("è" : "è".g)
| ("é" : "é".g)
| ("ê" : "ê".g)
| ("ì" : "ì".g)
| ("ò" : "ò".g)
| ("ù" : "ù".g)
];

gstar = Optimize[Project[UTF8_TO_GRAPHEME, 'output']*];

consonant =
  ("p".p) | ("b".p) | ("f".p) | ("m".p)
| ("t".p) | ("d".p) | ("l".p) | ("n".p)
| ("s".p) | ("z".p)
| ("r".p) | ("sy".p)
| ("th".p) | ("dh".p)
| ("c".p) | ("j".p) | ("y".p) | ("ny".p)
| ("k".p) | ("g".p) | ("kh".p) | ("w".p) | ("ng".p)
| ("'".p) | ("h".p)
;

vowel =
  ("i".p) |            ("u".p)
| ("e".p) |            ("o".p)
|           ("ex".p)
| ("a".p) |            ("ox".p)
;

g2p_consonant =
  ("p".g : "p".p)
| ("b".g : "b".p)
| ("f".g : "f".p)
| ("v".g : "f".p)
| ("m".g : "m".p)
| ("t".g : "t".p)
| ("d".g : "d".p)
| ("l".g : "l".p)
| ("n".g : "n".p <1>)
| ("s".g : "s".p)
| ("z".g : "z".p)
| ("r".g : "r".p)
| ("s y".g : "sy".p)  | ("s j".g : "sy".p) | ("s h".g : "sy".p)
| ("t h".g : "th".p)
| ("d h".g : "dh".p)
| ("c".g : "c".p)     | ("t j".g : "c".p)
| ("j".g : "j".p <1>) | ("d j".g : "j".p)
| ("y".g : "y".p <1>)
| ("n y".g : "ny".p)  | ("n j".g : "ny".p)
| ("k".g : "k".p)
| ("q".g : "k".p)
| ("x".g : "k s".p)
| ("g".g : "g".p)
| ("k h".g : "kh".p)
| ("c h".g : "kh".p)
| ("w".g : "w".p)
| ("n g".g : "ng".p)
| ("'".g : "'".p)
| ("h".g : "h".p <1>)
;

g2p_vowel_except_a =
  ("i".g : "i".p)
| ("ì".g : "i".p)  # ??
| ("u".g : "u".p)
| ("ù".g : "u".p)  # ??
| ("o e".g : "u".p <-1>)
| ("é".g : "e".p)
| ("o".g : "o".p)
| ("e".g : "ex".p)
| ("è".g : "e".p)
| ("ê".g : "e".p)  # ??
| ("ò".g : "ox".p)  # ??
| ("å".g : "ox".p)  # ??
| ("â".g : "ox".p)  # ??
;

rm_hyphen = ("-".g : "".p <-1>)*;

g2p_nonfinal_syllable =
 rm_hyphen
 (g2p_consonant)*
 (g2p_vowel_except_a | ("a".g : "a".p <1>))
 (g2p_consonant <1>)*
;

g2p_final_syllable_with_a =
 rm_hyphen
 (g2p_consonant)*
 ("a".g : "ox".p)
;

export G2P = Optimize[
  g2p_nonfinal_syllable*
  g2p_final_syllable_with_a*
  ("k".g : "'".p <-1>)?
];
